#  amsynth CMakeLists.txt
#
#  Copyright (C) 2021 AnClark Liu
#
#  This program is free software; you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation; either version 2 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program; if not, write to the Free Software
#  Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.

cmake_minimum_required (VERSION 3.7)

project (amsynth
  VERSION 0.1.0
  DESCRIPTION "Analogue Modeling SYNTHesizer"
  LANGUAGES C CXX
  HOMEPAGE_URL "https://github.com/amsynth/amsynth"
)

set (CMAKE_CXX_STANDARD 14)

#
# Global workarounds
#

# Enable -fPIC to let static library link properly
#
# On GNU glibc, -fPIC is required, or the following error may occur:
#    relocation R_X86_64_PC32 against symbol `stderr@@GLIBC_2.2.5' can not be used
#    when making a shared object; recompile with -fPIC
#
# MinGW-w64 also supports this flag. Even though on Windows, with or without this flag
# does not matter, we can still use it as well.
if (NOT MSVC)
    add_compile_options (-fPIC)
endif ()


#
# Path definitions
#

if (WIN32)
    set (DEFAULT_VST_PATH "$ENV{SYSTEMDRIVE}/Program Files/Steinberg/VSTPlugins")
    set (DEFAULT_LV2_PATH "$ENV{SYSTEMDRIVE}/Program Files/Common Files/LV2")
    set (DEFAULT_READONLY_DATADIR "${CONFIG_PREFIX}")
    set (DEFAULT_PKGDATADIR "${CONFIG_PREFIX}")
else ()
    set (DEFAULT_VST_PATH "/usr/lib/vst")
    set (DEFAULT_LV2_PATH "/usr/lib/lv2")
    set (DEFAULT_READONLY_DATADIR "${CONFIG_DATADIR}/amsynth/")
    set (DEFAULT_PKGDATADIR "${CONFIG_DATADIR}/amsynth/")
endif ()

set (CONFIG_READONLY_DATADIR "${DEFAULT_READONLY_DATADIR}" CACHE STRING "Directory for read-only architecture-independent data. HARDCODED in binaries.")
set (CONFIG_PKGDATADIR "${DEFAULT_PKGDATADIR}" CACHE STRING "Directory for storing package data (banks, etc.). HARDCODED in binaries.")

set (CONFIG_VST_PATH ${DEFAULT_VST_PATH} CACHE STRING "Specify VST plugin path")
set (CONFIG_LV2_PATH ${DEFAULT_LV2_PATH} CACHE STRING "Specity LV2 plugin path")

# Usually, these parameters are generated by autotools.
# In CMake, I need to feed them myself.
set (AMSYNTH_COMPILE_DEFINITIONS
    PRIVATE DATADIR="${CONFIG_READONLY_DATADIR}"
    PRIVATE PKGDATADIR="${CONFIG_PKGDATADIR}"
    PRIVATE PACKAGE="${PROJECT_NAME}"
    PRIVATE PACKAGE_NAME="${PROJECT_NAME}"
    PRIVATE PACKAGE_STRING="${PROJECT_NAME}"
    PRIVATE VERSION="${PROJECT_VERSION}"
    PRIVATE PACKAGE_URL="${PROJECT_HOMEPAGE_URL}"
    PRIVATE PACKAGE_BUGREPORT=""
)


#
# Import DPF support
#

set (DPF_PATH "${PROJECT_SOURCE_DIR}/vendor/dpf")
set (DPF_WIDGETS_PATH "${PROJECT_SOURCE_DIR}/vendor/dpf-widgets")
add_subdirectory (${DPF_PATH})


#
# Source files
#

set (LIBCORE_SRC
    src/Configuration.cpp
    src/Configuration.h
    src/controls.h
    src/filesystem.cpp
    src/filesystem.h
    src/midi.h
    src/MidiController.cpp
    src/MidiController.h
    src/Parameter.cpp
    src/Parameter.h
    src/Preset.cpp
    src/Preset.h
    src/PresetController.cpp
    src/PresetController.h
    src/types.h
    src/UpdateListener.h
)

set (LIBDSP_SRC
    src/Effects/Distortion.cpp
    src/Effects/Distortion.h
    src/Effects/SoftLimiter.cpp
    src/Effects/SoftLimiter.h
    src/Synthesizer.cpp
    src/Synthesizer.h
    src/TuningMap.cpp
    src/TuningMap.h
    src/VoiceAllocationUnit.cpp
    src/VoiceAllocationUnit.h
    src/VoiceBoard/ADSR.cpp
    src/VoiceBoard/ADSR.h
    src/VoiceBoard/LowPassFilter.cpp
    src/VoiceBoard/LowPassFilter.h
    src/VoiceBoard/Oscillator.cpp
    src/VoiceBoard/Oscillator.h
    src/VoiceBoard/Synth--.h
    src/VoiceBoard/VoiceBoard.cpp
    src/VoiceBoard/VoiceBoard.h
    vendor/freeverb/allpass.cpp
    vendor/freeverb/allpass.hpp
    vendor/freeverb/comb.cpp
    vendor/freeverb/comb.hpp
    vendor/freeverb/denormals.h
    vendor/freeverb/revmodel.cpp
    vendor/freeverb/revmodel.hpp
    vendor/freeverb/tuning.h
)

add_library (${PROJECT_NAME}_core STATIC
    ${LIBCORE_SRC}
    ${LIBDSP_SRC}
)

target_compile_definitions (${PROJECT_NAME}_core ${AMSYNTH_COMPILE_DEFINITIONS})

target_include_directories (${PROJECT_NAME}_core PUBLIC src)
target_include_directories (${PROJECT_NAME}_core PUBLIC vendor)


#
# Dear ImGui addons
#

set (IMGUI_ADDON_SRC
    vendor/imgui_addons/zynlab/imgui_common.cpp
    vendor/imgui_addons/zynlab/imgui_knob.cpp
    vendor/imgui_addons/anclark/imgui_extra_button.cpp
    vendor/imgui_addons/anclark/imgui_oscilloscope.cpp
    vendor/imgui_addons/imgui-knobs/imgui-knobs.cpp
)
set (IMGUI_ADDON_INCLUDE_DIR
    vendor/imgui_addons/zynlab
    vendor/imgui_addons/anclark
    vendor/imgui_addons/imgui-knobs
)

add_library (${PROJECT_NAME}_imgui_addons STATIC ${IMGUI_ADDON_SRC})

target_include_directories(${PROJECT_NAME}_imgui_addons PUBLIC ${DPF_WIDGETS_PATH}/opengl/DearImGui)
target_include_directories(${PROJECT_NAME}_imgui_addons PUBLIC ${IMGUI_ADDON_INCLUDE_DIR})


#
# Built-in fonts
#

set (FONT_SOURCE_DIR ${CMAKE_SOURCE_DIR}/vendor/fonts)
set (GENERATED_FONT_DIR ${CMAKE_BINARY_DIR}/generated_fonts)

# Binary-to-C convertor from Dear ImGui
add_executable (binary_to_compressed EXCLUDE_FROM_ALL ${FONT_SOURCE_DIR}/utils/binary_to_compressed_c.cpp)
target_link_options (binary_to_compressed PRIVATE -static)

# Handle cross-build stuff
# On native build, users can directly assign executable target name as param of add_custom_command(),
# but cross-build does not support this feature.
if (CMAKE_CROSSCOMPILING)
    set_target_properties (binary_to_compressed PROPERTIES
        RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}
    )
    set (binary_to_compressed_PATH ./binary_to_compressed${CMAKE_EXECUTABLE_SUFFIX})
else ()
    set (binary_to_compressed_PATH binary_to_compressed)
endif ()

# This is FontAudio icon font by Michelangelo Nottoli (https://github.com/fefanto/fontaudio)
set (INCLUDED_FONT_FONTAUDIO ${FONT_SOURCE_DIR}/icon-fonts/fontaudio.ttf)
add_custom_command (OUTPUT ${GENERATED_FONT_DIR}/fontaudio.h
                    COMMAND ${binary_to_compressed_PATH} "${INCLUDED_FONT_FONTAUDIO}" fontaudio > "${GENERATED_FONT_DIR}/fontaudio.h"
                    DEPENDS binary_to_compressed VERBATIM)
add_custom_target (generate_font_fontaudio DEPENDS ${GENERATED_FONT_DIR}/fontaudio.h)
# And, this is UTF-8 codepoint definition file from IconFontCppHeaders (https://github.com/juliettef/IconFontCppHeaders)
# Copy it to generated font directory
configure_file (${FONT_SOURCE_DIR}/icon-fonts/IconsFontaudio.h ${GENERATED_FONT_DIR}/IconsFontaudio.h COPYONLY)


#
# Dear ImGui editor UI sources
# These source files contain codes of ImGui-based UI construction.
# Separated from amsynth_dpf_ui.cpp for clarity.
#

set (EDITOR_UI_SOURCES
    src/ImGui/EditorUI.cpp
    src/ImGui/EditorUI_MainWindow.cpp
    src/ImGui/EditorUI_Controllers.cpp
    src/ImGui/EditorUI_Panels.cpp
)


#
# DPF target
#

dpf_add_plugin (${PROJECT_NAME}
  TARGETS clap lv2 vst2 vst3 jack
  FILES_DSP
      src/amsynth_dpf.cpp
  FILES_UI
      src/amsynth_dpf_ui.cpp
      ${DPF_WIDGETS_PATH}/opengl/DearImGui.cpp
      ${EDITOR_UI_SOURCES}
)

target_include_directories (${PROJECT_NAME} PUBLIC src)
target_include_directories (${PROJECT_NAME} PUBLIC ${DPF_WIDGETS_PATH}/generic)
target_include_directories (${PROJECT_NAME} PUBLIC ${DPF_WIDGETS_PATH}/opengl)

# Include header directory of ImGui
target_include_directories (${PROJECT_NAME} PUBLIC ${DPF_WIDGETS_PATH}/opengl/DearImGui)
target_include_directories (${PROJECT_NAME} PUBLIC vendor)  # ImGui addons

# Built-in fonts
add_dependencies (${PROJECT_NAME} generate_font_fontaudio)
target_include_directories (${PROJECT_NAME} PUBLIC ${GENERATED_FONT_DIR})

target_link_libraries (${PROJECT_NAME} PRIVATE ${PROJECT_NAME}_core)
target_link_libraries (${PROJECT_NAME} PRIVATE ${PROJECT_NAME}_imgui_addons)

# Workaround for SDL2 linker error on Windows (with Msys2)
if (WIN32)
    target_link_libraries(${PROJECT_NAME} PRIVATE "cfgmgr32" "imm32" "setupapi" "version")   # To deal with SDL2 linker error on Msys2
endif ()


#
# Factory bank conversion - convert factory banks to C++ code so that they can be accessed internally
#

add_subdirectory(utils/factory_bank_to_c)

# Handle cross-build stuff
if (CMAKE_CROSSCOMPILING)
    # Move factory_bank_to_c executable to CMAKE_BINARY_DIR for convenience
    set_target_properties (factory_bank_to_c PROPERTIES
        RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}
    )
    set (factory_bank_to_c_PATH ./factory_bank_to_c${CMAKE_EXECUTABLE_SUFFIX})
else ()
    set (factory_bank_to_c_PATH factory_bank_to_c)
endif ()

set (GENERATED_FACTORY_BANK_DIR "${CMAKE_BINARY_DIR}/factory_bank")
file (MAKE_DIRECTORY ${GENERATED_FACTORY_BANK_DIR})

set (SOURCES_FACTORY_BANK
    ${GENERATED_FACTORY_BANK_DIR}/amsynth_factory_banks.cpp
    ${GENERATED_FACTORY_BANK_DIR}/amsynth_factory_banks.h
)
set (SOURCES_FACTORY_BANK_HANDLER
    src/EmbedPresetController.cpp
    src/EmbedPresetController.h
)

add_custom_command (
    OUTPUT ${SOURCES_FACTORY_BANK}
    COMMAND ${factory_bank_to_c_PATH} ${PROJECT_SOURCE_DIR}/data/banks ${GENERATED_FACTORY_BANK_DIR}
    DEPENDS factory_bank_to_c
    VERBATIM
)

# Factory bank content and handler module
add_library (${PROJECT_NAME}_factory_bank STATIC
    ${SOURCES_FACTORY_BANK}
    ${SOURCES_FACTORY_BANK_HANDLER}
)
target_include_directories (${PROJECT_NAME}_factory_bank PRIVATE src)

target_link_libraries (${PROJECT_NAME} PRIVATE ${PROJECT_NAME}_factory_bank)
